Public selectedFolder As String

Private Sub cmdBrowse_Click()
    txtSelectFolder.Value = SelectFolder
End Sub

Private Sub txtSelectFolder_Change()
    selectedFolder = txtSelectFolder.Value
End Sub

Private Sub cmdCancel_Click()
    Unload Me
End Sub

Private Sub cmdStart_Click()
    Dim nwb As Workbook
    Dim nws As Worksheet
    
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.Calculation = xlCalculationManual
    
    Set nwb = Workbooks.Add
    Set nws = nwb.Sheets(1)
    nws.Range("A1").Value = "Kommunenr"
    nws.Range("B1").Value = "Bedriftsnr"
    nws.Range("C1").Value = "Bedriftsnr"
    nws.Range("D1").Value = "#"
    nws.Range("E1").Value = "Fødselsnr"
    nws.Range("F1").Value = "Dato fra"
    nws.Range("G1").Value = "#"
    nws.Range("H1").Value = "Navn"
    nws.Range("I1").Value = "Dato til"
    nws.Range("J1").Value = "Beløp"
    
    If cboSelectType.Value = "Normal" Then
       ImportNormalFiles nws
    ElseIf cboSelectType.Value = "NAV refusjon" Then
       ImportCsvFiles nws
    End If
        
    Application.EnableEvents = True
    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True
    
    MsgBox "Fullført!"
End Sub

Sub ImportNormalFiles(nws As Worksheet)
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim fileExtension As String
    Dim folderFiles As String
    Dim lastRowIndex As Integer
    
    fileExtension = "*.xlsx"
    folderFiles = Dir(selectedFolder & fileExtension)
        
    Do While folderFiles <> ""
        Set wb = Workbooks.Open(Filename:=selectedFolder & folderFiles)
        Set ws = wb.Sheets(1)
            
        lastRowIndex = nws.Cells(nws.Rows.Count, "A").End(xlUp).row
                    
        ws.UsedRange.Copy
        nws.Cells(lastRowIndex + 1, "A").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
        SkipBlanks:=False, Transpose:=False
            
        wb.Close SaveChanges:=False
        folderFiles = Dir
    Loop
End Sub

Sub ImportCsvFiles(nws As Worksheet)
    Dim fileExtension As String
    Dim filePath As String
    Dim lastRowIndex As Integer
    Dim line As String
    Dim col As Integer
    Dim arrayOfElements
    Dim element As Variant

    fileExtension = "*.csv"
    folderFiles = Dir(selectedFolder & fileExtension)
    
    Do While folderFiles <> ""
        filePath = selectedFolder & folderFiles
            
        lastRowIndex = nws.Cells(nws.Rows.Count, "A").End(xlUp).row
            
        col = 0
        Open filePath For Input As #1
            Do While Not EOF(1)
                Line Input #1, line
                arrayOfElements = Split(line, ";")
                    
                For Each element In arrayOfElements
                    col = col + 1
                    nws.Cells(lastRowIndex + 1, col).Value = element
                Next
                    
                lastRowIndex = lastRowIndex + 1
                col = 0
            Loop
        Close #1
            
        folderFiles = Dir
    Loop
    
    nws.Columns("A").NumberFormat = "0000"
    nws.Columns("E").NumberFormat = "00000000000"
    
    Const x As Integer = 100
    Dim cell As Range
    Dim rng As Range
    
    Set rng = nws.Range("J2", Range("J6000").End(xlUp)).SpecialCells(xlCellTypeConstants)
    For Each cell In rng
        cell.Value = cell.Value / x
    Next cell
    
    nws.Cells(lastRowIndex + 1, "J").Value = Application.Sum(Range(Cells(2, "j"), Cells(lastRowIndex, "j")))
End Sub

Private Sub UserForm_Initialize()
    txtSelectFolder.Value = ""
    
    cboSelectType.Clear
    
    With cboSelectType
        .AddItem "Normal"
        .AddItem "NAV refusjon"
        .ListIndex = 0
    End With
End Sub

Function SelectFolder() As String
    Dim dialogFolder As FileDialog
    Dim folderName As String
    
    Set dialogFolder = Application.FileDialog(msoFileDialogFolderPicker)
    With dialogFolder
        .Title = "Velg en mappe"
        .AllowMultiSelect = False
        If .Show <> -1 Then GoTo NextCode
        folderName = .SelectedItems(1) & "\"
    End With
    
NextCode:
    Set dialogFolder = Nothing
    SelectFolder = folderName
End Function
